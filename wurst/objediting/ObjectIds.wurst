package ObjectIds
import NoWurst
import ErrorHandling
import Wurstunit
import Assets
import LinkedList
import StringUtils
import TypeCasting

constant CHARMAP = ".................................!.#$%&'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ[.]^_`abcdefghijklmnopqrstuvwxyz{|}~................................................................................................................................."

@deprecated("Use .toRawCode() instead.") public function int2fourchar(int value) returns string
	return toRawCode(value)

/** Convert a integer id value into a 4-letter id code. */
public function toRawCode(int value) returns string
	var result = ""
	var remainingValue = value

	for int byteno = 0 to 3
		let charValue = remainingValue mod 256
		remainingValue = remainingValue div 256
		result = CHARMAP.charAt(charValue) + result

	return result

@deprecated("Use .fromRawCode() instead.") public function fourchar2int(string value) returns int
	return fromRawCode(value)

/** converts a 4-letter id code to an integer */
public function fromRawCode(string value) returns int
	var result = 0
	if value.length() != 4
		error("Fourchar value must be 4 chars long")
	else
		var factor = 1
		for i = 0 to 3
			let pos = CHARMAP.indexOf(value.charAt(3-i))
			result += factor*pos
			factor *= 256
	
	return result	
	
public function string.fromRawCode() returns int
	return fromRawCode(this)
	
public function int.toRawCode() returns string
	return toRawCode(this)

@test function test_idString2IdInteger()
	fromRawCode("hfoo") .assertEquals('hfoo')
	fromRawCode("abcd") .assertEquals('abcd')
	fromRawCode("ABCD") .assertEquals('ABCD')
	
@test function test_idInteger2IdString()
	toRawCode('hfoo') .assertEquals("hfoo")
	toRawCode('abcd') .assertEquals("abcd")
	toRawCode('ABCD') .assertEquals("ABCD")

public function asList(vararg int ids) returns string
	let list = new LinkedList<int>
	for id in ids
		list.add(id)
	let result = list.joinBy((int i) -> i.toRawCode(), ",")
	destroy list
	return result

public function asList(vararg string values) returns string
	let list = new LinkedList<string>
	for v in values
		list.add(v)
	let result = list.joinBy(",")
	destroy list
	return result

@Test function asListIntTest()
	asList(
		AbilityIds.invulnerable,
		AbilityIds.reveal
	).assertEquals("Avul,Adta")

@Test function asListStringTest()
	asList(
		"self",
		"structure"
	).assertEquals("self,structure")